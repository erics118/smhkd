# note
# ( AAA )* means 0 or more of AAA
# ( AAA )+ means 1 or more of AAA
# AAA | BBB means AAA or BBB but not both

# config properties ============================================================================

max_chord_interval := max time between chords
hold_modifier_threshold := min time for a keysym to be held to be considered as a <held_mod>
simultaneous_threshold := max time between keysyms to be considered as a <simultaneous_keysym>

config_property_name := 'max_chord_interval' | 
                       'hold_modifier_threshold' | 
                       'simultaneous_threshold'

config_property := <config_property_name> '=' <number>

# keysym =======================================================================================
key         := single letter or number # a-z, 0-9
keycode     := hex code corresponding to macOS virtual keycode # 0x00-0xFF
literal     := predefined keyword # enter, space, escape, etc

identifier  := a string of letters, numbers, and underscores, starting with a letter
number      := a sequence of digits

mouse       := 'left' | 'right' | 'middle'

# a simple keysym
simple_keysym := <key> | <keycode> | <literal> | <mouse>

# two or more simple keysyms pressed at the same time
simultaneous_keysym := '[' <simple_keysym> (',' <simple_keysym>)+ ']'

# a keysym with brace expansion, allowing for easily binding multiple keysyms
brace_expansion_keysym := '{' <simple_keysym> (',' <simple_keysym>)+ '}'

# keysym
keysym      := <simple_keysym> | <brace_expansion_keysym> | <simultaneous_keysym>

# modifiers ====================================================================================
base_mod    := 'cmd' | 'lcmd' | 'rcmd' | 
               'alt' | 'lalt' | 'ralt' | 
               'shift' | 'lshift' | 'rshift' | 
               'ctrl' | 'lctrl' | 'rctrl' | 
               'fn'

# custom modifier for easily binding multiple modifiers
# can recursively define other custom modifiers
custom_mod  := 'define_modifier' <identifier> '=' <modifiers>

# having a simple keysym to act as a modifier
held_mod    := '(' <simple_keysym> ')'

modifier    := <base_mod> | <custom_mod> | <held_mod> | <modifier> '+' <modifier>

# flags ========================================================================================
# allows event to pass through to other applications
passthrough := '@'

# enables repeat based on macos settings, cannot be combined with ^
repeat      := '&'

# activates on release rather than press
on_release  := '^'

# flags can be combined with other flags, but only in certain ways
# other combinations not allowed
flags       := <passthrough> | <repeat> | <on_release> | 
               <passthrough> <repeat> | <repeat> <passthrough>

# command ======================================================================================
command     := any valid shell command on a single line. supports brace expansions

# chords =======================================================================================
# a chord is a keysym, or modifier plus a keysym
chord       := <keysym> | <modifier> '+' <keysym>

# a list of chords, separated by semicolons
# can have flags at the end
chords      := <chord> (';' <chord>)* |
               <chord> (';' <chord>)* <flags>

# hotkey is a list of chords followed by a command
# there can be a newline after the ':' for readability
hotkey      := <chords> ':' <command>

# a config file contains only:
#     <config_property>
#     <custom_modifier>
#     <hotkey>
